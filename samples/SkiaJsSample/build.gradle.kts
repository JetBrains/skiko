plugins {
    // Cannot move to 1.5.31 due to
    // "Cannot change attributes of dependency configuration ':commonTestApiDependenciesMetadata' after it has been resolved"
    kotlin("multiplatform") version "1.5.20"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven("https://dl.bintray.com/kotlin/kotlin-eap")
    maven("https://maven.pkg.jetbrains.space/public/p/compose/dev")
}

var version = "0.0.0-SNAPSHOT"
if (project.hasProperty("skiko.version")) {
    version = project.properties["skiko.version"] as String
}

val resourcesDir = "$buildDir/resources/"

val skikoWasm by configurations.creating

dependencies {
    skikoWasm("org.jetbrains.skiko:skiko-js-wasm-runtime:$version")
}

val unzipTask = tasks.register("unzipWasm", Copy::class) {
    destinationDir = file(resourcesDir)
    from(skikoWasm.map { zipTree(it) })
}

tasks.withType<org.jetbrains.kotlin.gradle.dsl.KotlinJsCompile>().configureEach {
    dependsOn(unzipTask)
}

kotlin {

    js(IR) {
        browser()
        binaries.executable()
    }

    sourceSets {
        val commonMain by getting {}

        val jsMain by getting {
            dependsOn(commonMain)
            dependencies {
                // This one is tricky - it has js and wasm binaries required for final linking.
                // We cannot use it directly but need to extract data from there.
                implementation("org.jetbrains.skiko:skiko-js-runtime:$version")
            }
            resources.setSrcDirs(resources.srcDirs + unzipTask.get().destinationDir)
        }
    }
}

// a temporary workaround for a bug in jsRun invocation - see https://youtrack.jetbrains.com/issue/KT-48273
afterEvaluate {
    extensions.configure<org.jetbrains.kotlin.gradle.targets.js.nodejs.NodeJsRootExtension> {
        versions.webpackDevServer.version = "4.0.0"
    }
}

