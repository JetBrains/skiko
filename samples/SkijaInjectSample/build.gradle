plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'application'
}

repositories {
    mavenLocal()
    jcenter()
    maven {
       url 'https://dl.bintray.com/kotlin/kotlin-eap'
    }
    maven {
       url 'https://packages.jetbrains.team/maven/p/ui/dev'
    }
}

def osName = System.getProperty("os.name")
def targetOs = ""
if (osName == "Mac OS X") {
    targetOs = "macos"
} else if (osName.startsWith("Win")) {
    targetOs = "windows"
} else if (osName.startsWith("Linux")) {
    targetOs = "linux"
} else {
    throw Error("Unsupported OS: $os")
}
def osArch = System.getProperty("os.arch")
def targetArch = ""
if (osArch == "x86_64") {
    targetArch = "x64"
} else if (osArch == "aarch64") {
    targetArch = "arm64"
} else {
    throw Error("Unsupported arch: $osArch")
}
def target = "${targetOs}_${targetArch}"

def version = "0.1-SNAPSHOT"
if (project.hasProperty('skiko.version')) {
  version = project.properties['skiko.version']
}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation "org.jetbrains.skiko:skiko-jvm-runtime-$target:$version"
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
}

application {
    mainClassName = 'SkijaInjectSample.AppKt'
}
